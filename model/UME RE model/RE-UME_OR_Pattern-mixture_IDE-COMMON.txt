###############################################################################################
#                                                                                             #
#                         Random-effects unrelated mean effects model                         #
#                      <Binomial likelihood, logit link, Random Effects>                      #
#             (Improved parameterisation  of Dias et al., 2013 - PMID: 23804508)              #
#                               One-stage pattern-mixture model                               #
#                           (Spineli et al., 2019 - PMID: 31018836)                           #
#      Odds ratio (OR) and Informative Missingness Odds ratio (IMOR) in logarithmic scale     #
#                         <Identical, common-within network log IMOR>                         #
#                           (Turner et al., 2015 - PMID: 25809313)                            #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){
     delta[i, 1] <- 0
     w[i, 1] <- 0

     ## Baseline log odds of event
     logit(p[i, 1]) <- u[i] 
     u[i] ~ dnorm(0, .0001)

     for(k in 1:na[i]){  
       ## Observed events and likelihood per arm
       r[i, k] ~ dbin(p_o[i, k], obs[i, k])
       obs[i, k] <- N[i, k] - m[i, k]
       p_o[i, k] <- max(0, min(1, ((-((q[i, k] - p[i, k])*(1 - theta[i, k]) - 1) - sqrt((pow(((q[i, k] - p[i, k])*(1 - theta[i, k]) - 1), 2)) - 
                                  ((4*p[i, k])*(1 - q[i, k])*(1 - theta[i, k]))))/(2*(1 - q[i, k])*(1 - theta[i, k]))))) 

       ## Missing participants and likelihood per arm
       m[i, k] ~ dbin(q[i, k], N[i, k])
       q[i, k] ~ dunif(0, 1)

       ## Identical, common-within-network logIMOR 
       log(theta[i, k]) <- phi


       ## Deviance and residuals
       hat.par[i, k] <- rhat[i, k]
       rhat[i, k] <- p_o[i, k]*obs[i, k]                                 # Expected value of numerators (observed data; PMID: 25809313)
       dev.o[i, k] <- 2*(r[i, k]*(log(r[i, k]) - log(rhat[i, k]))        # Deviance contribution (observed data; PMID: 23104435)
                      + (obs[i, k] - r[i, k])*(log(obs[i, k] - r[i, k]) 
                      - log(obs[i, k] - rhat[i, k]))) 
       m0[i, k] <- m[i, k] + 0.01*equals(m[i, k], 0)                     # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
       hat.m[i, k] <- q[i, k]*N[i, k]                                    # Expected value of numerators (missing data; PMID: 25809313)
       dev.m[i, k] <- 2*(m0[i, k]*(log(m0[i, k]) - log(hat.m[i, k]))     # Deviance contribution (MOD; PMID: 25809313)
                      + (N[i, k] - m0[i, k])*(log(N[i, k] - m0[i, k])  
                      - log(N[i, k] - hat.m[i, k])))
                      }

     resdev.o[i] <- sum(dev.o[i, 1:2])                                    # Summed residual deviance contribution for this trial (observed data; PMID: 23104435)
     resdev.m[i] <- sum(dev.m[i, 1:2])                                    # Summed residual deviance contribution for this trial (MOD; PMID: 25809313)

    for(k in 2:na[i]){
      logit(p[i, k]) <- u[i] + delta[i, k]
      delta[i, k] ~ dnorm(md[i, k], precd[i, k])
      md[i, k] <- EM[t[i, k], t[i, 1]] + sw[i, k]
      w[i, k] <- delta[i, k] - EM[t[i, k], t[i, 1]]
      sw[i, k] <- sum(w[i, 1:(k - 1)])/(k - 1)
      precd[i, k] <- 2*(k - 1)*prec/k
                    }
                }


    ## Comparisons between non-baseline interventions studied only in multi-arm trials
    for(i in 1:nbase.multi){
      EM[t2.bn[i], t1.bn[i]] <- EM[t2.bn[i], base[i]] - EM[t1.bn[i], base[i]]
                              }

    totresdev.o <- sum(resdev.o[])                                        # Total Residual Deviance (observed data; PMID: 23104435)
    totresdev.m <- sum(resdev.m[])                                        # Total Residual Deviance (MOD; PMID: 25809313)

    ## Prior consideration on identical, common-within-network logIMOR
    phi ~ dnorm(meand.phi, precd.phi)
 

    ## Obtain the estimates for the observed comparisons only
    for(i in 1:N.obs){
       EM[t2[i], t1[i]] ~ dnorm(0, .0001)
                 }


    ## Weakly-informative (or empirically-based) prior distribution on between-trial standard deviation (variance)
    prec <- pow(tau, -2)                                                                  
    tau.a ~ dnorm(0, heter.prior[2])I(0, ) 
    tau.b ~ dunif(0, heter.prior[2])
    logtau2 ~ dlnorm(heter.prior[1], heter.prior[2])
    tau <- pow(exp(logtau2), 0.5)*(1 - equals(heter.prior[3], 1))*(1 - equals(heter.prior[3], 2)) + 
           tau.a*equals(heter.prior[3], 1) + tau.b*equals(heter.prior[3], 2)
    } # END OF MODEL






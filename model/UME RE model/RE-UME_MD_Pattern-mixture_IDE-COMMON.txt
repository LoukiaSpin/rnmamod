###############################################################################################
#                                                                                             #
#                         Random-effects unrelated mean effects model                         #
#                     <Normal likelihood, identity link, Random Effects>                      #
#             (Improved parameterisation  of Dias et al., 2013 - PMID: 23804508)              #
#                               One-stage pattern-mixture model                               #
#                           (Spineli et al., 2021 - PMID: 33406990)                           #
#        Mean Difference (MD) and Informative Missingness Difference of Means (IMDOM)         #
#                           (Mavridis et al., 2015 - PMID: 25393541)                          #
#                           <Identical, common-within-network IMDOM>                          #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){                                                       # loop through trials
      delta[i, 1] <- 0
      w[i, 1] <- 0

      ## Baseline mean value
      theta[i, 1] <- u[i]
      u[i] ~ dnorm(0, 0.0001)

      for(k in 1:na[i]){                                                  # loop through all arms in trial i
        ## Observed mean value and precision per arm & linking equation
        prec.o[i, k] <- pow(se.o[i, k], -2)                               # observed precision of the outcome
        y.o[i, k] ~ dnorm(theta.o[i, k], prec.o[i, k])                    # normal likelihood for the oberved mean of the outcome
        theta.o[i, k] <- theta[i, k] - phi*q[i, k]                        # linking equation for Informative Missingness Difference of Means (IMDoM)

        ## Missing participants and likelihood per arm
        m[i, k] ~ dbin(q[i, k], N[i, k])                                  # binomial likelihood for missing outcome data (MOD)
        q[i, k] ~ dunif(0, 1)                                             # uniform likelihood for the probability of MOD

        ## Deviance contribution for observed and missing data
        hat.par[i, k] <- theta.o[i, k]
        dev.o[i, k] <- (y.o[i, k] - theta.o[i, k])*(y.o[i, k] -           # Deviance contribution (observed data; PMID: 23104435)
                        theta.o[i, k])*prec.o[i, k]
        m0[i, k] <- m[i, k] + 0.01*equals(m[i, k], 0)                     # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
        hat.m[i, k] <- q[i, k]*N[i, k]                                    # Expected value of numerators (missing data; PMID: 25809313)
        dev.m[i, k] <- 2*(m0[i, k]*(log(m0[i, k]) - log(hat.m[i, k])) +   # Deviance contribution (MOD; PMID: 25809313)
                       (N[i, k] - m0[i, k])*(log(N[i, k] - m0[i, k]) - 
                       log(N[i, k] - hat.m[i, k])))
                       }

      resdev.o[i] <- sum(dev.o[i, 1:na[i]])                               # Summed residual deviance contribution for this trial (observed data; PMID: 23104435)
      resdev.m[i] <- sum(dev.m[i, 1:na[i]])                               # Summed residual deviance contribution for this trial (MOD; PMID: 25809313)
      
      for(k in 2:na[i]){                                       
        theta[i, k] <- u[i] + delta[i, k]                                 # link function for mean difference (MD)
        delta[i, k] ~ dnorm(md[i, k], precd[i, k])                        # within-trial true MD (random-effect)
        md[i, k] <- EM[t[i, k], t[i, 1]] + sw[i, k]                       # mean of MD distributions (with multi-arm trial correction)
        precd[i, k] <- 2*prec*(k - 1)/k                                   # precision of MD distributions (with multi-arm trial correction)
        w[i, k] <- delta[i, k] - EM[t[i, k], t[i, 1]]                     # adjustment for multi-arm trials
        sw[i, k] <- sum(w[i, 1:(k - 1)])/(k - 1)                          # cumulative adjustment for multi-arm trials
                       }
                    }


    ## Comparisons between non-baseline interventions studied only in multi-arm trials
    for(i in 1:nbase.multi){
      EM[t2.bn[i], t1.bn[i]] <- EM[t2.bn[i], base[i]] - EM[t1.bn[i], base[i]]
                              }


    totresdev.o <- sum(resdev.o[])                                        # Total Residual Deviance (observed data; PMID: 23104435)
    totresdev.m <- sum(resdev.m[])                                        # Total Residual Deviance (MOD; PMID: 25809313)


    ## Prior consideration on identical, common-within-network IMDOM
    phi ~ dnorm(meand.phi, precd.phi)                                              


    ## Obtain the estimates for the observed comparisons only
    for(i in 1:N.obs){
       EM[t2[i], t1[i]] ~ dnorm(0, .0001)
                 }


    ## Weakly-informative prior distribution on between-trial standard deviation 
    prec <- pow(tau, -2)                                                                  
    tau.a ~ dnorm(0, heter.prior[2])I(0, ) 
    tau.b ~ dunif(0, heter.prior[2])
    tau <- tau.a*equals(heter.prior[3], 1) + tau.b*equals(heter.prior[3], 2)  
    } # END OF MODEL  

                              
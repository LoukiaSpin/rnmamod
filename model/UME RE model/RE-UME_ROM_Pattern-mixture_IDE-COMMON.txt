###############################################################################################
#                                                                                             #
#                         Random-effects unrelated mean effects model                         #
#             (Improved parameterisation  of Dias et al., 2013 - PMID: 23804508)              #
#                        <Normal likelihood, log link, Random Effects>                        #
#                (Extension of Dias et al., 2013 in Appendix - PMID: 23104435)                #
#       [Trial set 1: change from baseline & baseline; Trial set 2: final measurements]       #
#                               One-stage pattern-mixture model                               #
#                           (Spineli et al., 2021 - PMID: 33406990)                           #
#           Ratio of Means (ROM) and Informative Missingness Ratio of Means (IMROM)           #
#                           (Mavridis et al., 2015 - PMID: 25393541)                          #
#                           <Identical, common-within-network IMROM>                          #
#                                                                                             #
###############################################################################################



model{
   for(i in 1:ns){                                                                                          # loop through trials
     theta[i, 1] <- u[i]
     u[i] ~ dnorm(0, 0.0001)
     delta[i, 1] <- 0
     w[i, 1] <- 0
   }


   for(i in 1:n1){                                                                                           # loop through trials reporting at final point 
     for(k in 1:na[i]){                                                                                      # loop through all arms in trial i
       prec.o[i, k] <- pow(se.o[i, k], -2)                                                                   # observed precision of the outcome
       y.o[i, k] ~ dnorm(theta.o[i, k], prec.o[i, k])                                                        # normal likelihood for the oberved mean of the outcome
       m[i, k] ~ dbin(q[i, k], N[i, k])                                                                      # binomial likelihood for missing outcome data (MOD)
       q[i, k] ~ dunif(0, 1)                                                                                 # uniform likelihood for the probability of MOD
     
       theta.o[i, k] <- theta[i, k]/(1 - q[i, k]*(1 - exp(phi)))                                             # linking equation for Informative Missingness Ratio of Means (IMRoM)                                                                               

       ## Deviance contribution for observed and missing data
       hat.par[i, k] <- theta.o[i, k]
       dev.o[i, k] <- (y.o[i, k] - theta.o[i, k])*(y.o[i, k] -                                               # Deviance contribution (observed data; PMID: 23104435)
                        theta.o[i, k])*prec.o[i, k]
       m0[i, k] <- m[i, k] + 0.01*equals(m[i, k], 0)                                                         # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
       hat.m[i, k] <- q[i, k]*N[i, k]                                                                        # Expected value of numerators (missing data; PMID: 25809313)
       dev.m[i, k] <- 2*(m0[i, k]*(log(m0[i, k]) - log(hat.m[i, k])) +                                       # Deviance contribution (MOD; PMID: 25809313)
                      (N[i, k] - m0[i, k])*(log(N[i, k] - m0[i, k]) - 
                      log(N[i, k] - hat.m[i, k])))
                       }
                    }


    for(i in 1:n2){                                                                                           # loop through trials reporting the change from baseline and baseline 
       for(k in 1:na[i + n1]){                                                                                # loop through all arms in trial i
         prec.b[i + n1, k] <- pow(se.b[i + n1, k], -2)                                                        # precision of the outcome at baseline
         prec.o[i + n1, k] <- pow(se.o[i + n1, k], -2)                                                        # precision of the observed change from baseline
         y.b[i + n1, k] ~ dnorm(theta.b[i + n1, k], prec.b[i + n1, k])                                        # normal likelihood of the outcome at baseline
         y.o[i + n1, k] ~ dnorm(md.cond[i + n1, k], prec.cond[i + n1, k])                                     # normal likelihood of the observed change from baseline conditional on the baseline
         md.cond[i + n1, k] <- theta.o[i + n1, k] + sqrt(prec.b[i + n1, k]/prec.o[i + n1, k])*rho*(y.b[i + n1, k] # conditional mean of the observed change from baseline conditional on the baseline 
                               - theta.b[i + n1, k])
         prec.cond[i + n1, k] <- prec.o[i + n1, k]/(1 - rho*rho)
         m[i + n1, k] ~ dbin(q[i + n1, k], N[i + n1, k])                                                      # binomial likelihood for missing outcome data (MOD)
         q[i + n1, k] ~ dunif(0, 1)                                                                           # uniform likelihood for the probability of MOD

         theta.cfb[i + n1, k] <- theta[i + n1, k] - theta.b[i + n1, k]                                        # Underlying change from baseline for the randomised sample in arm k 
         theta.b[i + n1, k] ~ dnorm(0, 0.0001)

         ## Sharing IMRoM for change from baseline with IMRoM at final point
         ## (we have no reason to believe that the missingness mechanism (MM) for change from baseline differs from the MM for the final point)
         theta.o[i + n1, k] <- theta.cfb[i + n1, k]/(1 - q[i + n1, k]*(1 - exp(phi)))                         # linking equation for Informative Missingness Ratio of Means (IMRoM)

         ## Deviance contribution for observed and missing data (change from baseline)
         hat.par[i + n1, k] <- theta.o[i + n1, k]
         dev.o[i + n1, k] <- (y.o[i + n1, k] - theta.o[i + n1, k])*(y.o[i + n1, k] -                          # Deviance contribution (observed data; PMID: 23104435)
                             theta.o[i + n1, k])*prec.o[i + n1, k]
         m0[i + n1, k] <- m[i + n1, k] + 0.01*equals(m[i + n1, k], 0)                                         # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
         hat.m[i + n1, k] <- q[i + n1, k]*N[i + n1, k]                                                        # Expected value of numerators (missing data; PMID: 25809313)
         dev.m[i + n1, k] <- 2*(m0[i + n1, k]*(log(m0[i + n1, k]) - log(hat.m[i + n1, k])) +                  # Deviance contribution (MOD; PMID: 25809313)
                             (N[i + n1, k] - m0[i + n1, k])*(log(N[i + n1, k] - m0[i + n1, k]) - 
                             log(N[i + n1, k] - hat.m[i + n1, k])))
                       }
                    }


   for(i in 1:ns){   

     resdev.o[i] <- sum(dev.o[i, 1:na[i]])                                                                   # Summed residual deviance contribution for this trial (observed data; PMID: 23104435)
     resdev.m[i] <- sum(dev.m[i, 1:na[i]])                                                                   # Summed residual deviance contribution for this trial (MOD; PMID: 25809313)
      
     for(k in 2:na[i]){                                       
       theta[i, k] <- u[i]*exp(delta[i, k])                                                                   # link function for ratio of means (RoM) in logarithmic scale
       delta[i, k] ~ dnorm(md[i, k], taud[i, k])                                                              # within-trial true log RoM (random-effect)
       md[i, k] <- EM[t[i, k], t[i, 1]] + sw[i, k]                                                            # mean of log RoM distributions (with multi-arm trial correction)
       taud[i, k] <- 2*prec*(k - 1)/k                                                                         # precision of log RoM distributions (with multi-arm trial correction)
       w[i, k] <- delta[i, k] - EM[t[i, k], t[i, 1]]                                                          # adjustment for multi-arm trials
       sw[i, k] <- sum(w[i, 1:(k - 1)])/(k - 1)                                                               # cumulative adjustment for multi-arm trials
                      }
                   }


    ## Comparisons between non-baseline interventions studied only in multi-arm trials
    for(i in 1:nbase.multi){
      EM[t2.bn[i], t1.bn[i]] <- EM[t2.bn[i], base[i]] - EM[t1.bn[i], base[i]]
                              }


   totresdev.o <- sum(resdev.o[])                                                                            # Total Residual Deviance (observed data; PMID: 23104435)
   totresdev.m <- sum(resdev.m[])                                                                            # Total Residual Deviance (MOD; PMID: 25809313)
 

    ## Obtain the estimates for the observed comparisons only
    for(i in 1:N.obs){
       EM[t2[i], t1[i]] ~ dnorm(0, .0001)
                 }


   phi ~ dnorm(meand.phi, precd.phi)                                            # identical, common-within-network log IMRoM                                                                                  

   ## Weakly-informative prior distribution on between-trial standard deviation 
   prec <- pow(tau, -2)                                                                  
   tau.a ~ dnorm(0, heter.prior[2])I(0, ) 
   tau.b ~ dunif(0, heter.prior[2])
   tau <- tau.a*equals(heter.prior[3], 1) + tau.b*equals(heter.prior[3], 2)  
     }  

                              
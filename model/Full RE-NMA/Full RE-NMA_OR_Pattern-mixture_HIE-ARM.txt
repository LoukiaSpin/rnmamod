###############################################################################################
#                                                                                             #
#       Random-effects NMA model with consistency and accommodation of multi-arm trials       #
#                      <Binomial likelihood, logit link, Random Effects>                      #
#                      (Dias et al., 2013 in Appendix - PMID: 23104435)                       #
#                               One-stage pattern-mixture model                               #
#                           (Spineli et al., 2019 - PMID: 31018836)                           #
#      Odds ratio (OR) and Informative Missingness Odds ratio (IMOR) in logarithmic scale     #
#                       <Hierarchical, intervention-specific log IMOR>                        #
#                           (Turner et al., 2015 - PMID: 25809313)                            #
#              <<Transitive assumed missingness mechanism across interventions>>              #
#                                (Spineli 2019 - PMID: 30223064)                              #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){
      w[i, 1] <- 0

      ## Baseline log odds of event
      logit(p[i, 1]) <- u[i] 
      u[i] ~ dnorm(0, .0001)

      for(k in 1:na[i]){  
        ## Observed events and likelihood per arm
        r[i, k] ~ dbin(p_o[i, k], obs[i, k])
        obs[i, k] <- N[i, k] - m[i, k]
        p_o[i, k] <- max(0, min(1, ((-((q[i, k] - p[i, k])*(1 - theta[i, k]) - 1) - sqrt((pow(((q[i, k] - p[i, k])*(1 - theta[i, k]) - 1), 2)) - 
                                   ((4*p[i, k])*(1 - q[i, k])*(1 - theta[i, k]))))/(2*(1 - q[i, k])*(1 - theta[i, k]))))) 

        ## Missing participants and likelihood per arm
        m[i, k] ~ dbin(q[i, k], N[i, k])
        q[i, k] ~ dunif(0, 1)
  
        ## Hierarchical, intervention-specific logIMOR 
        log(theta[i, k]) <- phi[i, k]
        phi[i, k] ~ dnorm(mean.phi[t[i, k]], prec.phi[t[i, k]])

        ## Deviance and residuals
        hat.par[i, k] <- rhat[i, k]
        rhat[i, k] <- p_o[i, k]*obs[i, k]                                 # Expected value of numerators (observed data; PMID: 25809313)
        dev.o[i, k] <- 2*(r[i, k]*(log(r[i, k]) - log(rhat[i, k]))        # Deviance contribution (observed data; PMID: 23104435)
                       + (obs[i, k] - r[i, k])*(log(obs[i, k] - r[i, k]) 
                       - log(obs[i, k] - rhat[i, k]))) 
        m0[i, k] <- m[i, k] + 0.01*equals(m[i, k], 0)                     # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
        hat.m[i, k] <- q[i, k]*N[i, k]                                    # Expected value of numerators (missing data; PMID: 25809313)
        dev.m[i, k] <- 2*(m0[i, k]*(log(m0[i, k]) - log(hat.m[i, k]))     # Deviance contribution (MOD; PMID: 25809313)
                       + (N[i, k] - m0[i, k])*(log(N[i, k] - m0[i, k])  
                       - log(N[i, k] - hat.m[i, k])))
                    }

     resdev.o[i] <- sum(dev.o[i, 1:na[i]])                                # Summed residual deviance contribution for this trial (observed data; PMID: 23104435)
     resdev.m[i] <- sum(dev.m[i, 1:na[i]])                                # Summed residual deviance contribution for this trial (MOD; PMID: 25809313)

    for(k in 2:na[i]){
      logit(p[i, k]) <- u[i] + delta[i, k]
      delta[i, k] ~ dnorm(md[i, k], precd[i, k])
      md[i, k] <- d[t[i, k]] - d[t[i, 1]] + sw[i, k]
      w[i, k] <- delta[i, k] - (d[t[i, k]] - d[t[i, 1]])
      sw[i, k] <- sum(w[i, 1:(k - 1)])/(k - 1)
      precd[i, k] <- 2*(k - 1)*prec/k
                     }
                 }

    totresdev.o <- sum(resdev.o[])                                        # Total Residual Deviance (observed data; PMID: 23104435)
    totresdev.m <- sum(resdev.m[])                                        # Total Residual Deviance (MOD; PMID: 25809313)


    ## Prior consideration on hyperparameters of hierarchical, intervention-specific logIMOR
    # Scenario for the reference intervention
    mean.phi[ref] ~ dnorm(meand.phi[2], precd.phi)
    prec.phi[ref] <- pow(sd.phi[ref], -2)
    sd.phi[ref] ~ dunif(0, psi.phi)

    # Scenario for the non-reference interventions
    for(t in 1:(ref - 1)){
      mean.phi[t] ~ dnorm(meand.phi[1], precd.phi)
      prec.phi[t] <- pow(sd.phi[t], -2)
      sd.phi[t] ~ dunif(0, psi.phi)
                  }

    for(t in (ref + 1):nt){
      mean.phi[t] ~ dnorm(meand.phi[1], precd.phi)
      prec.phi[t] <- pow(sd.phi[t], -2)
      sd.phi[t] ~ dunif(0, psi.phi)
                  }
    psi.phi <- pow(precd.phi, -2)

    ## Basic parameters - prior distributions
    d[ref] <- 0
    for(t in 1:(ref - 1)){
      d[t] ~ dnorm(0, .0001)
                         }

    for(t in (ref + 1):nt){
      d[t] ~ dnorm(0, .0001)
                          }

    ## Ranking probabilities and SUCRA values
    sorted <- rank(d[])
    for(t in 1:nt){
      order[t] <- (nt + 1 - sorted[t])*equals(D, 1) + sorted[t]*(1 - equals(D, 1))
      most.effective[t] <- equals(order[t], 1)

      for(l in 1:nt){
        effectiveness[t, l] <- equals(order[t], l)
        cumeffectiveness[t, l] <- sum(effectiveness[t, 1:l])
                     }

      SUCRA[t] <- sum(cumeffectiveness[t, 1:(nt - 1)])/(nt - 1)
                   }  

    ## Obtain reference-specific logORs (effect measure; EM) and its prediction 
    for(t in 1:(ref - 1)){
      EM.ref[t] <- d[t] - d[ref]
      pred.ref[t] ~ dnorm(EM.ref[t], prec)
                          }

    for(t in (ref + 1):nt){
      EM.ref[t] <- d[t] - d[ref]
      pred.ref[t] ~ dnorm(EM.ref[t], prec)
                          }

    ## Obtain all possible (unique) pairwise comparisons and their prediction
    for(c in 1:(nt - 1)){
      for(k in (c + 1):nt){
        EM[k, c] <- d[k] - d[c]
        EM.pred[k, c] ~ dnorm(EM[k, c], prec)
                          }
                         }  

    ## Weakly-informative (or empirically-based) prior distribution on between-trial standard deviation (variance)
    prec <- pow(tau, -2)                                                                  
    tau.a ~ dnorm(0, heter.prior[2])I(0, ) 
    tau.b ~ dunif(0, heter.prior[2])
    logtau2 ~ dlnorm(heter.prior[1], heter.prior[2])
    tau <- pow(exp(logtau2), 0.5)*(1 - equals(heter.prior[3], 1))*(1 - equals(heter.prior[3], 2)) + 
           tau.a*equals(heter.prior[3], 1) + tau.b*equals(heter.prior[3], 2)
    } # END OF MODEL






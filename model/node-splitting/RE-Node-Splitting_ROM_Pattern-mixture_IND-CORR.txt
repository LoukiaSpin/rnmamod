###############################################################################################
#                                                                                             #
#        Random-effects Node-splitting approach and accommodation of multi-arm trials         #
#                             (Dias et al., 2010 – PMID: 20213715)                            #
#                     <Normal likelihood, identity link, Random Effects>                      #
#                      (Dias et al., 2013 in Appendix  - PMID: 23104435)                      #
#       [Trial set 1: change from baseline & baseline; Trial set 2: final measurements]       # 
#                               One-stage pattern-mixture model                               #
#                           (Spineli et al., 2021 - PMID: 33406990)                           #
#           Ratio of Means (ROM) and Informative Missingness Ratio of Means (IMROM)           #
#                           (Mavridis et al., 2015 - PMID: 25393541)                          #
#                         <Independent, within-trial correlated IMROM>                        #
#                                                                                             #
###############################################################################################



model{
    for(i in 1:ns){                                                                         # loop through trials
      w[i, 1] <- 0
      j[i, 1] <- 0
      delta[i, bi[i]] <- 0

      ## Baseline mean value
      u[i] ~ dnorm(0, 0.0001)
    }


    for(i in 1:n1){                                                                         # loop through trials reporting at final point
      for(k in 1:na[i]){                                                                    # loop through all arms in trial i
        prec.o[i, k] <- pow(se.o[i, k], -2)                                                 # observed precision of the outcome
        y.o[i, k] ~ dnorm(theta.o[i, k], prec.o[i, k])                                      # normal likelihood for the oberved mean of the outcome
        theta.o[i, k] <- theta[i, k]/(1 - q[i, k]*(1 - exp(phi[i, k])))                     # linking equation for Informative Missingness Ratio of Means (IMRoM)

        ## Missing participants and likelihood per arm
        mod[i, k] ~ dbin(q[i, k], N[i, k])                                                  # binomial likelihood for missing outcome data (MOD)
        q[i, k] ~ dunif(0, 1)                                                               # uniform likelihood for the probability of MOD   
 
        for(l in 1:na[i]){
          V[i, k, l] <- cov.phi*(1 - equals(k, l)) + var.phi*equals(k, l)                   # variance-covariance matrix for multivariate log IMRoM (variance specified in R script and correlation equal to 0.5)
                        }
                       }
                    }


    for(i in 1:n2){                                                                                               # loop through trials reporting the change from baseline and baseline 
       for(k in 1:na[i + n1]){                                                                                    # loop through all arms in trial i
         prec.b[i + n1, k] <- pow(se.b[i + n1, k], -2)                                                            # precision of the outcome at baseline
         prec.o[i + n1, k] <- pow(se.o[i + n1, k], -2)                                                            # precision of the observed change from baseline
         y.b[i + n1, k] ~ dnorm(theta.b[i + n1, k], prec.b[i + n1, k])                                            # normal likelihood of the outcome at baseline
         y.o[i + n1, k] ~ dnorm(md.cond[i + n1, k], prec.cond[i + n1, k])                                         # normal likelihood of the observed change from baseline conditional on the baseline
         md.cond[i + n1, k] <- theta.o[i + n1, k] + sqrt(prec.b[i + n1, k]/prec.o[i + n1, k])*rho*(y.b[i + n1, k] # conditional mean of the observed change from baseline conditional on the baseline 
                               - theta.b[i + n1, k])
         prec.cond[i + n1, k] <- prec.o[i + n1, k]/(1 - rho*rho)
         m[i + n1, k] ~ dbin(q[i + n1, k], N[i + n1, k])                                                          # binomial likelihood for missing outcome data (MOD)
         q[i + n1, k] ~ dunif(0, 1)                                                                               # uniform likelihood for the probability of MOD

         theta.cfb[i + n1, k] <- theta[i + n1, k] - theta.b[i + n1, k]                                            # Underlying change from baseline for the randomised sample in arm k 
         theta.b[i + n1, k] ~ dnorm(0, 0.0001)

         ## Sharing IMRoM for change from baseline with IMRoM at final point
         ## (we have no reason to believe that the missingness mechanism (MM) for change from baseline differs from the MM for the final point)
         theta.o[i + n1, k] <- theta.cfb[i + n1, k]/(1 - q[i + n1, k]*(1 - exp(phi[i + n1, k])))                  # linking equation for Informative Missingness Ratio of Means (IMRoM)

        for(l in 1:na[i + n1]){
          V[i + n1, k, l] <- cov.phi*(1 - equals(k, l)) + var.phi*equals(k, l)                   # variance-covariance matrix for multivariate log IMRoM (variance specified in R script and correlation equal to 0.5)
                        }
                       }
                    }


    for(i in 1:ns){   
     for(k in 1:na[i]){ 
        theta[i, k] <- u[i]*exp(delta[i, t[i, k]])                                    # link function for ratio of means (RoM) in logarithmic scale
        index[i, k] <- split[i]*(equals(t[i, k], pair[1])+ equals(t[i, k], pair[2]))                     
                       }

      Omega[i, 1:na[i], 1:na[i]] <- inverse(V[i, 1:na[i], 1:na[i]])                         # Precision matrix for multivariate log IMRoM
      phi[i, 1:na[i]] ~ dmnorm(M[i, 1:na[i]], Omega[i, 1:na[i], 1:na[i]])                   # independent, multivariate log IMRoM
     
      for(k in 2:na[i]){                                       
        delta[i, si[i, k]] ~ dnorm(md[i, si[i, k]], precd[i, si[i, k]])                     # within-trial true log RoM (random-effect)
        md[i, si[i, k]] <- (d[si[i, k]] - d[bi[i]] + sw[i, k])*(1 - index[i, m[i, k]])      # mean of log RoM distributions (with multi-arm trial correction)                         
                            + direct*index[i, m[i, k]]  
        j[i, k] <- k - (equals(1, split[i])*step(k - 3))                                    # adjusting for the correlated log RoMs for arms removed to split node                
        precd[i, si[i, k]] <- 2*prec*(j[i, k] - 1)/j[i, k]                                  # precision of log RoM distributions (with multi-arm trial correction)
        w[i, k] <- delta[i, si[i, k]] - (d[si[i, k]] + d[bi[i]])*(1 - index[i, k])          # adjustment for multi-arm trials
        sw[i, k] <- sum(w[i, 1:(k - 1)])/(j[i, k] - 1)                                      # cumulative adjustment for multi-arm trials
                      }
                   }

    ## Basic parameters - prior distributions
    d[ref] <- 0                                                                             # reference intervention in the network
    for(t in 1:(ref - 1)){
      d[t] ~ dnorm(0, 0.0001)                                                               # normal distribution for basic parameters
                          }

    for(t in (ref + 1):nt){
      d[t] ~ dnorm(0, 0.0001)                                                               # normal distribution for basic parameters
                          }


    ## Obtain all possible (unique) pairwise comparisons in logarithmic scale 
    for(c in 1:nt){  
      for(k in 1:nt){ 
        EM[c, k] <- d[k] - d[c] 
                          }  
                        }

    direct ~ dnorm(0, .0001) 
    diff <- direct - EM[pair[2], pair[1]]                                                 # calculate the difference between 'direct' and indirect 'log RoM' (pair[2] vs pair[1])
    prob <- step(diff)                                                                    # calculate p-value for inconsistency factor


    ## Weakly-informative prior distribution on between-trial standard deviation 
    prec <- pow(tau, -2)                                                                  
    tau.a ~ dnorm(0, heter.prior[2])I(0, ) 
    tau.b ~ dunif(0, heter.prior[2])
    tau <- tau.a*equals(heter.prior[3], 1) + tau.b*equals(heter.prior[3], 2)          
    } # END OF MODEL  

                              
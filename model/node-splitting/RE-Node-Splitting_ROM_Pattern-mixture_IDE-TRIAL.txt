###############################################################################################
#                                                                                             #
#        Random-effects Node-splitting approach and accommodation of multi-arm trials         #
#                             (Dias et al., 2010 – PMID: 20213715)                            #
#                     <Normal likelihood, identity link, Random Effects>                      #
#                      (Dias et al., 2013 in Appendix  - PMID: 23104435)                      #
#                               One-stage pattern-mixture model                               #
#                           (Spineli et al., 2021 - PMID: 33406990)                           #
#           Ratio of Means (ROM) and Informative Missingness Ratio of Means (IMROM)           #
#                           (Mavridis et al., 2015 - PMID: 25393541)                          #
#                              <Identical, trial-specific IMROM>                              #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){                                                                         # loop through trials
      w[i, 1] <- 0
      j[i, 1] <- 0
      delta[i, bi[i]] <- 0

      ## Baseline mean value
      u[i] ~ dnorm(0, 0.0001)

      for(k in 1:na[i]){                                                                    # loop through all arms in trial i
        ## Observed mean value and precision per arm & linking equation
        prec.o[i, k] <- pow(se.o[i, k], -2)                                                 # observed precision of the outcome
        y.o[i, k] ~ dnorm(theta.o[i, t[i, k]], prec.o[i, k])                                # normal likelihood for the oberved mean of the outcome
        theta.o[i, k] <- theta[i, k]/(1 - q[i, k]*(1 - exp(phi[i, k])))                     # linking equation for Informative Missingness Ratio of Means (IMRoM)

        ## Missing participants and likelihood per arm
        mod[i, k] ~ dbin(q[i, t[i, k]], N[i, k])                                            # binomial likelihood for missing outcome data (MOD)
        q[i, t[i, k]] ~ dunif(0, 1)                                                         # uniform likelihood for the probability of MOD

        theta[i, t[i, k]] <- u[i]*exp(delta[i, t[i, k]])                                    # link function for ratio of means (RoM) in logarithmic scale
        index[i, k] <- split[i]*(equals(t[i, k], pair[1])+ equals(t[i, k], pair[2]))                     
                       }
      
      for(k in 2:na[i]){                                       
        delta[i, si[i, k]] ~ dnorm(md[i, si[i, k]], precd[i, si[i, k]])                     # within-trial true log RoM (random-effect)
        md[i, si[i, k]] <- (d[si[i, k]] - d[bi[i]] + sw[i, k])*(1 - index[i, m[i, k]])      # mean of log RoM distributions (with multi-arm trial correction)                         
                            + direct*index[i, m[i, k]]  
        j[i, k] <- k - (equals(1, split[i])*step(k - 3))                                    # adjusting for the correlated log RoMs for arms removed to split node                
        precd[i, si[i, k]] <- 2*prec*(j[i, k] - 1)/j[i, k]                                  # precision of log RoM distributions (with multi-arm trial correction)
        w[i, k] <- delta[i, si[i, k]] - (d[si[i, k]] + d[bi[i]])*(1 - index[i, k])          # adjustment for multi-arm trials
        sw[i, k] <- sum(w[i, 1:(k - 1)])/(j[i, k] - 1)                                      # cumulative adjustment for multi-arm trials
                      }
                   }

    ## Basic parameters - prior distributions
    d[ref] <- 0                                                                             # reference intervention in the network
    for(t in 1:(ref - 1)){
      d[t] ~ dnorm(0, 0.0001)                                                               # normal distribution for basic parameters
                         }

    for(t in (ref + 1):nt){
      d[t] ~ dnorm(0, 0.0001)                                                               # normal distribution for basic parameters
                          }

    ## Prior consideration on identical, trial-specific log IMROM
    for(i in 1:ns){
      phi[i] ~ dnorm(meand.phi, precd.phi)                                                          
                  } 


    ## Obtain all possible (unique) pairwise comparisons in logarithmic scale 
    for(c in 1:nt){  
      for(k in 1:nt){ 
        EM[c, k] <- d[k] - d[c] 
                          }  
                       }

    direct ~ dnorm(0, .0001) 
    diff <- direct - EM[pair[2], pair[1]]                                                   # calculate the difference between 'direct' and indirect 'log RoM' (pair[2] vs pair[1])
    prob <- step(diff)                                                                      # calculate p-value for inconsistency factor


    ## Weakly-informative prior distribution on between-trial standard deviation 
    prec <- pow(tau, -2)                                                                  
    tau.a ~ dnorm(0, heter.prior[2])I(0, ) 
    tau.b ~ dunif(0, heter.prior[2])
    tau <- tau.a*equals(heter.prior[3], 1) + tau.b*equals(heter.prior[3], 2)           
    } # END OF MODEL  

                              